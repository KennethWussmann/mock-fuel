plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'jacoco'
    id 'org.jmailen.kotlinter' version '1.26.0'
    id 'io.gitlab.arturbosch.detekt' version '1.0.1'
    id 'maven-publish'
    id 'com.palantir.git-version' version '0.12.2'
}

version versionDetails().lastTag
group 'net.wussmann.kenneth'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.1'
    compile 'com.github.kittinunf.fuel:fuel:2.2.1'
    compileOnly 'org.junit.jupiter:junit-jupiter-api:5.5.1'

    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.5.1'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testCompile 'org.jetbrains.kotlin:kotlin-test:1.3.41'
    testCompile 'io.mockk:mockk:1.9.3'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

publishing {
    repositories {
        maven {
            credentials {
                username "KennethWussmann"
                password System.getenv('MVN_DEPLOY_PWD')
            }
            url = "https://maven.pkg.github.com/KennethWussmann/mock-fuel"
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'mock-fuel'
                description = 'Mocking Fuel HTTP requests in Kotlin with ease'
                url = 'https://kenneth.wussmann.net/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/KennethWussmann/mock-fuel/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'KennethWussmann'
                        name = 'Kenneth Wu√ümann'
                        email = 'kenneth@wussmann.net'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/KennethWussmann/mock-fuel.git'
                    developerConnection = 'scm:git:git@github.com:KennethWussmann/mock-fuel.git'
                    url = 'https://github.com/KennethWussmann/mock-fuel'
                }
            }
        }
    }
}

detekt {
    input = files('src/main/kotlin')
    filters = '.*/resources/.*,.*/build/.*,.*test.*,.*/model/.*'
    config = files('detekt-config.yml')
}

jacoco {
    toolVersion = '0.8.4'
}

test {
    dependsOn lintKotlin
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}