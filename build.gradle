plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'jacoco'
    id 'org.jmailen.kotlinter' version '1.26.0'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0-RC15'
    id 'maven-publish'
}

group 'net.wussmann.kenneth'
version '1.2.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M1'
    compile 'com.github.kittinunf.fuel:fuel:2.1.0'
    compileOnly 'org.junit.jupiter:junit-jupiter-api:5.5.0-RC1'

    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.0'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.5.0-RC1'
    testCompile 'org.jetbrains.kotlin:kotlin-test:1.3.41'
    testCompile 'io.mockk:mockk:1.9.3'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

publishing {
    repositories {
        maven {
            credentials {
                username System.getenv('MVN_DEPLOY_USR')
                password System.getenv('MVN_DEPLOY_PWD')
            }
            def repo = 'https://repo.ketrwu.host'
            if (!project.hasProperty('release')) {
                version += '-SNAPSHOT'
                url = "$repo/repository/snapshots"
            } else {
                url = "$repo/repository/releases"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'mock-fuel'
                description = 'Mocking Fuel HTTP requests in Kotlin with ease'
                url = 'https://kenneth.wussmann.net/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/KennethWussmann/mock-fuel/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'KennethWussmann'
                        name = 'Kenneth Wu√ümann'
                        email = 'kenneth@wussmann.net'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/KennethWussmann/mock-fuel.git'
                    developerConnection = 'scm:git:git@github.com:KennethWussmann/mock-fuel.git'
                    url = 'https://github.com/KennethWussmann/mock-fuel'
                }
            }
        }
    }
}

detekt {
    input = files('src/main/kotlin')
    filters = '.*/resources/.*,.*/build/.*,.*test.*,.*/model/.*'
    config = files('detekt-config.yml')
}

jacoco {
    toolVersion = '0.8.4'
}

test {
    dependsOn lintKotlin
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}