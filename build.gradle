plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.70"
    id "jacoco"
    id "org.jmailen.kotlinter" version "2.3.0"
    id "io.gitlab.arturbosch.detekt" version "1.1.1"
    id "maven-publish"
    id "com.palantir.git-version" version "0.12.2"
}

version versionDetails().lastTag
group "net.wussmann.kenneth"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    def junitVersion = "5.6.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3-native-mt"
    implementation "com.github.kittinunf.fuel:fuel:2.2.1"
    compileOnly "org.junit.jupiter:junit-jupiter-api:$junitVersion"

    testCompile "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test:1.3.70"
    testCompile "io.mockk:mockk:1.10.3-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    repositories {
        maven {
            credentials {
                username "KennethWussmann"
                password System.getenv("GITHUB_TOKEN")
            }
            url = "https://maven.pkg.github.com/KennethWussmann/mock-fuel"
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "mock-fuel"
                description = "Mocking Fuel HTTP requests in Kotlin with ease"
                url = "https://kenneth.wussmann.net/"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://raw.githubusercontent.com/KennethWussmann/mock-fuel/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "KennethWussmann"
                        name = "Kenneth Wu√ümann"
                        email = "kenneth@wussmann.net"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/KennethWussmann/mock-fuel.git"
                    developerConnection = "scm:git:git@github.com:KennethWussmann/mock-fuel.git"
                    url = "https://github.com/KennethWussmann/mock-fuel"
                }
            }
        }
    }
}

detekt {
    input = files("src/main/kotlin")
    filters = ".*/resources/.*,.*/build/.*,.*test.*,.*/model/.*"
    config = files("detekt-config.yml")
}

jacoco {
    toolVersion = "0.8.5"
}

test {
    dependsOn lintKotlin
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
    }
}
